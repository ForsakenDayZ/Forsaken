/*
Base Manager for DayZ Epoch
Created by maca134

Allows you to copy/paste, save structures and insert them into the game.

This is for personal use only and must only be used fairly!

*/

// Predefined structures
BCBaseList = [
	[
		"base1",
		"3x4 2 Floor Base",
		[0,23,0],
		[
["MetalFloor_DZ",[-0.458008,-0.708984,6.58927],358.325],
["MetalFloor_DZ",[-0.6875,-0.491211,3.21856],178.332],
["MetalFloor_DZ",[4.51172,-0.341797,3.21756],88.3251],
["MetalFloor_DZ",[-0.611328,4.56738,6.58885],358.325],
["MetalFloor_DZ",[4.82031,-0.555664,6.58885],88.325],
["MetalFloor_DZ",[-0.887695,4.78906,3.21856],268.325],
["MetalFloor_DZ",[-5.73438,-0.864258,6.5897],358.325],
["MetalFloor_DZ",[-0.587891,-5.77148,3.21756],178.332],
["MetalFloor_DZ",[-0.302734,-5.98633,6.5897],88.325],
["MetalFloor_DZ",[-5.98828,-0.651367,3.21956],358.332],
["MetalFloor_DZ",[4.41211,4.93848,3.21856],88.3251],
["MetalFloor_DZ",[4.66602,4.72168,6.58842],268.325],
["CinderWall_DZ",[7.21191,-0.261719,3.36756],88.3251],
["MetalFloor_DZ",[4.71191,-5.61133,3.21756],268.325],
["MetalFloor_DZ",[-5.88965,4.41309,6.58927],358.325],
["CinderWall_DZ",[-0.988281,7.42871,3.36856],178.332],
["CinderWall_DZ",[7.6123,-0.0117188,-0.000439167],268.325],
["MetalFloor_DZ",[4.97461,-5.83203,6.58927],88.325],
["MetalFloor_DZ",[-6.1875,4.62891,3.21956],358.332],
["CinderWall_DZ",[-0.788086,7.78906,-0.000439167],178.332],
["MetalFloor_DZ",[-5.58008,-6.1416,6.59013],268.325],
["MetalFloor_DZ",[-5.8877,-5.92188,3.21956],358.332],
["CinderWall_DZ",[-0.1875,-8.51172,3.37056],358.332],
["CinderWallDoor_DZ",[-0.1875,-8.51172,-0.000439167],358.332],
["CinderWall_DZ",[-8.58789,-0.72168,3.36956],268.325],
["CinderWall_DZ",[7.01172,5.01855,3.36856],88.3251],
["CinderWall_DZ",[-8.6875,-0.500977,-0.000439167],268.325],
["CinderWall_DZ",[4.3125,7.57813,3.36856],358.332],
["CinderWall_DZ",[2.6123,-8.43164,3.37056],358.332],
["CinderWall_DZ",[7.3125,-5.54102,3.36756],268.325],
["CinderWall_DZ",[7.41211,5.48828,-0.000439167],268.325],
["CinderWall_DZ",[4.71191,7.94824,-0.000439167],178.332],
["CinderWall_DZ",[7.77246,-5.51465,-0.000439167],268.325],
["CinderWall_DZ",[-6.1875,7.26855,3.36956],358.332],
["CinderWall_DZ",[-6.28809,7.63867,-0.000439167],178.332],
["CinderWallDoorSmall_DZ",[5.3125,-8.35156,-0.000439167],358.332],
["CinderWall_DZ",[-8.78809,4.55859,3.36956],268.325],
["CinderWall_DZ",[-8.78809,4.99902,-0.000439167],268.325],
["Sandbag1_DZ",[-0.488281,-10.1514,3.25956],88.7339],
["Sandbag1_DZ",[2.3125,-10.0615,3.15856],267.998],
["WoodLadder_DZ",[4.64941,-9.22363,3.50016],86.7948],
["CinderWallDoorSmall_DZ",[-5.6875,-8.67188,-0.000439167],178.332],
["CinderWall_DZ",[-8.48828,-6.00098,3.37056],268.325],
["CinderWall_DZ",[-8.48828,-6.00098,-0.000439167],268.325],
["CinderWall_DZ",[-2.78809,-11.3418,-0.000439167],268.325],
["MetalFloor_DZ",[-2.78809,-11.3418,3.21756],268.325],
["CinderWallDoorSmall_DZ",[-2.78809,-11.3418,3.37056],268.325],
["MetalFloor_DZ",[4.91211,-10.8916,3.21656],178.332],
["CinderWallDoorSmall_DZ",[4.91211,-10.8916,3.21656],88.3251],
["Sandbag1_DZ",[2.3125,-12.0615,3.15856],269.105],
["Sandbag1_DZ",[-0.387695,-12.4111,3.25956],89.1047],
["WoodFloorHalf_DZ",[6.16113,-10.8564,6.44506],178.325],
["Sandbag1_DZ",[2.91211,-12.291,-0.00143909],87.5481],
["MetalFloor_DZ",[-5.42676,-11.4189,6.59055],178.325],
["WoodStairsSans_DZ",[-7.48828,-10.6016,0.299561],268.325],
["CinderWall_DZ",[7.51172,-10.8115,3.36656],88.3251],
["CinderWall_DZ",[7.91211,-11.0215,-0.000439167],268.325],
["CinderWall_DZ",[-8.28809,-11.501,-0.000439167],268.325],
["CinderWall_DZ",[-8.28809,-11.501,3.37056],268.325],
["CinderWallHalf_DZ",[-2.6875,-14.0918,3.37056],178.332],
["CinderWall_DZ",[4.91211,-13.5313,3.36656],178.332],
["CinderWall_DZ",[5.51172,-13.8516,-0.000439167],178.332],
["CinderWall_DZ",[-5.48828,-14.1719,-0.000439167],178.332],
["CinderWall_DZ",[-5.48828,-14.1719,3.37056],178.332],
["WoodStairsSans_DZ",[-7.28809,-14.0713,-2.60044],268.325]
		]
	]
];



if (isNil "BCCurrentBase") then {
	BCCurrentBase = [];
};

TraderDialogLoadItemList = {};
TraderDialogShowPrices = {};
TraderDialogSell = {};
TraderDialogBuy = {
	systemChat str [lbCurSel 12000, lbCurSel 12001];
	[lbCurSel 12000, lbCurSel 12001] spawn {
		private ["_pindex", "_bindex", "_base", "_player", "_base_objects"];
		systemChat str _this;
		_pindex = _this select 0;
		_bindex = _this select 1;
		if (_pindex < 0 or _bindex < 0) exitWith {};
		_base = BCBaseList select _bindex;
		_player = BCNearbyList select _pindex;
		_attach_position = player modelToWorld (_base select 2);
		_attach_position set [2, getPosASL player select 2];
		_base_objects = [_base select 3, _attach_position, _player] call fn_BCCreateBase;
		[_base_objects] call fn_BCBuildbase;
	};
};

fn_BCInsert = {
	createdialog "TraderDialog";
	lbClear 12000;
	lbClear 12001;
	ctrlShow [12005, false];
	{
		lbAdd [12001, format["%1 (%2)", _x select 1, _x select 0]];
		true
	} count BCBaseList;

	BCNearbyList = [];
	{
		if (_x isKindOf "Man" and !(_x isKindOf "zZombie_base")) then {
			BCNearbyList set [count BCNearbyList, _x];
			lbAdd [12000, format["%1", name _x]];
		};
		true
	} count (player nearEntities ["CAManBase", 10]);
};

fn_BCSetCenter = {
	private ["_centerSign"];
	BC_radius = nil;
	BC_Center = getPosASL player;
	_centerSign = createVehicle ["Sign_arrow_down_large_EP1", [0,0,0], [], 0, "CAN_COLLIDE"];
	_centerSign setPosASL BC_Center;
	_centerSign spawn {sleep 30; deleteVehicle _this;};
	showCommandingMenu "#USER:BCMainMenu";
};

fn_BCSetRadius = {
	if (isNil "BC_Center") exitWith
	{
		systemChat "Center not set";
	};
	BC_radius = [player, BC_center] call BIS_fnc_distance2D;
	showCommandingMenu "#USER:BCMainMenu";
	[] spawn {
		private ["_pos", "_radius", "_angle", "_distance", "_count", "_step", "_count", "_step", "_objects"];
		_angle = 0;
		_count = round((2 * pi * BC_radius) / 2);
		_objects = [];
		for "_x" from 0 to _count do
		{
			private["_a", "_b", "_obj"];
			_a = (BC_Center select 0) + (sin(_angle)*BC_radius);
			_b = (BC_Center select 1) + (cos(_angle)*BC_radius);
			_obj = createVehicle ["Sign_sphere100cm_EP1", [0,0,0], [], 0, "CAN_COLLIDE"];
			_obj setPosASL [_a, _b, BC_center select 2];
			_objects set [count _objects, _obj];
			_angle = _angle + (360/_count);
		};
		
		for "_x" from 0 to _count do
		{
			private["_a", "_b", "_obj"];
			_a = (BC_Center select 0) + (sin(_angle)*BC_radius);
			_b = (BC_Center select 2) + (cos(_angle)*BC_radius);
			_obj = createVehicle ["Sign_sphere100cm_EP1", [0,0,0], [], 0, "CAN_COLLIDE"];
			_obj setPosASL [_a, BC_center select 1, _b];
			_objects set [count _objects, _obj];
			_angle = _angle + (360/_count);
		};
		
		for "_x" from 0 to _count do
		{
			private["_a", "_b", "_obj"];
			_a = (BC_Center select 1) + (sin(_angle)*BC_radius);
			_b = (BC_Center select 2) + (cos(_angle)*BC_radius);
			_obj = createVehicle ["Sign_sphere100cm_EP1", [0,0,0], [], 0, "CAN_COLLIDE"];
			_obj setPosASL [BC_center select 0, _a, _b];
			_objects set [count _objects, _obj];
			_angle = _angle + (360/_count);
		};

		sleep 30;
		{ deleteVehicle _x; true } count _objects;
	};
};

fn_BCCopy = {
	private ["_objects", "_position", "_distance", "_nearest_objects"];
	if (isNil "BC_Center" or isNil "BC_radius") exitWith
	{
		systemChat "Center not set";
	};
	_objects = [];
	_position = BC_center;
	_distance = BC_radius;
	_nearest_objects = nearestObjects [[_position select 0, _position select 1], dayz_allowedObjects, _distance];
	diag_log "========= Copying Objects [start] =========";
	{
		private ["_obj_type", "_direction", "_obj_position", "_relative_position", "_row"];
		_obj_type = typeOf _x;
		_direction = getDir _x;
		_obj_position = getPosASL _x;
		_relative_position = [
			(_obj_position select 0) - (_position select 0),
			(_obj_position select 1) - (_position select 1),
			(_obj_position select 2) - (_position select 2)
		];
		_row = [_obj_type, _relative_position, _direction];
		diag_log str(_row);
		_row set [count _row, _x];
		_objects set [count _objects, _row];
		true
	} count _nearest_objects;
	systemChat format["Copied %1 items", count _nearest_objects];
	diag_log "========= Copying Objects [end] =========";
	BCCopiedBase = _objects;
	["<t size='0.6'>Base copied to logs</t>",0,0.8,0.5,0,0,8] spawn BIS_fnc_dynamicText;
	showCommandingMenu "#USER:BCMainMenu";
	_objects
};

fn_BCPaste = {
	private ["_dimensions", "_attach_position", "_base_objects"];
	if (isNil "BCCopiedBase") exitWith
	{
		systemChat "Base not copied not set";
	};
	_dimensions = BCCopiedBase call fn_BCGetDimensions;
	_attach_position = player modelToWorld [0, ((_dimensions select 0) max (_dimensions select 1)), 0];
	diag_log str [0, ((_dimensions select 0) max (_dimensions select 1)), 0];
	_attach_position set [2, getPosASL player select 2];
	_base_objects = [BCCopiedBase, _attach_position] call fn_BCCreateBase;
	[_base_objects] call fn_BCBuildbase;
};

fn_BCDelete = {
	BCConfirmBaseDelete = [
		["Are you sure?",true],
			["No", 	[2], "", -5, [["expression", ""]], "1", "1"],
			["Yes", 	[3], "", -5, [["expression", "[] spawn fn_BCConfirmDelete"]], "1", "1"]
	];
	showCommandingMenu "#USER:BCConfirmBaseDelete";
};

fn_BCConfirmDelete = {
	private ["_position", "_distance", "_nearest_objects"];
	if (isNil "BC_Center" or isNil "BC_radius") exitWith
	{
		systemChat "Center not set";
	};
	_position = BC_center;
	_distance = BC_radius;
	_nearest_objects = nearestObjects [[_position select 0, _position select 1], dayz_allowedObjects, _distance];
	{
		PVDZE_obj_Delete = [_objectID,_objectUID,_activatingPlayer];
		publicVariableServer "PVDZE_obj_Delete";
		deleteVehicle _x;
		true
	} count _nearest_objects;
	[format["<t size='0.6'>Deleted %1 objects</t>", count _nearest_objects],0,0.8,0.5,0,0,8] spawn BIS_fnc_dynamicText;
};

fn_BCSaveToDb = {
	{
		PVDZE_obj_Publish = [_x getVariable ["CharacterID", dayz_characterID],_x,[getDir _x, getPosATL _x], typeOf _x];
		publicVariableServer "PVDZE_obj_Publish";
	} count BCCurrentBase;
	[format["<t size='0.6'>Added %1 objects to database</t>", count BCCurrentBase],0,0.8,0.5,0,0,8] spawn BIS_fnc_dynamicText;
};

fn_BCCancelBase = {
	{
		detach _x; deleteVehicle _x;
	} count BCCurrentBase;
	BCCurrentBase = [];
	["<t size='0.6'>Cancelled</t>",0,0.8,0.5,0,0,8] spawn BIS_fnc_dynamicText;
};

fn_BCCenter = {
	private ["_objects", "_ax", "_ay", "_az", "_total"];
	_objects = [];
	_ax = 0;
	_ay = 0;
	_az = 0;
	{
		private ["_pos"];
		_pos = getPosASL _x;
		_ax = _ax + (_pos select 0);
		_ay = _ay + (_pos select 1);
		_az = _az + (_pos select 2);
	} count _this;
	_total = count _this;
	_center = [_ax / _total, _ay / _total, _az / _total];
	_center
};

fn_BCCreateBase = {
	private ["_objects", "_items", "_position", "_player"];
	_objects = [];
	_items = _this select 0;
	_position = _this select 1;
	if (count _this == 3) then {
		_player = _this select 2;
	};
	{
		private ["_object", "_orig_obj"];
		_object = createVehicle [_x select 0, [0,0,0], [], 0, "CAN_COLLIDE"];
		_object setPosASL [
			((_x select 1) select 0) + (_position select 0),
			((_x select 1) select 1) + (_position select 1),
			((_x select 1) select 2) + (_position select 2) 
		];
		_object attachTo [player];
		_object setDir ((_x select 2) - getDir player);
		if (count _x == 4) then {
			_orig_obj = _x select 3;
			_object setVariable ["CharacterID", _orig_obj getVariable ["CharacterID", ""], true];
		};
		if (!isNil "_player") then {
			_object setVariable ["CharacterID", (_player getVariable ["CharacterID","0"]), true];
		};
		_objects set [count _objects, _object];
		true
	} count _items;
	_objects
};

fn_BCBuildbase = {
	private ["_base_objects", "_finished", "_place"];
	_base_objects = _this select 0;
	{
		_x attachTo [player];
		true
	} count _base_objects;
	_finished = false;
	DZE_Q = false;
	DZE_Z = false;
	DZE_4 = false;
	DZE_6 = false;
	DZE_5 = false;
	DZE_cancelBuilding = false;
	_place = false;
	while {!_finished} do {
		private ["_player_direction"];
		["<t size='0.6'>SPACE: Place | Q/R: Rotate | PgUp/PgDn: Height</t>",0,0.8,0.5,0,0,8] spawn BIS_fnc_dynamicText;
		_player_direction = getDir player;
		if (DZE_Q or DZE_Z) then {
			{
				private ["_obj_direction", "_position"];
				detach _x;
				_obj_direction = getDir _x;
				_position = getPosASL _x;
				_position set [2, (_position select 2) + (if (DZE_Q) then {0.5} else {-0.5})];
				_x setPosASL _position;
				_x attachTo [player];
				_x setDir (_obj_direction - _player_direction);
				true
			} count _base_objects;
			DZE_Q = false;
			DZE_Z = false;
		};
		if (DZE_4 or DZE_6) then {
			private ["_center_position"];
			_center_position = _base_objects call fn_BCCenter;
			{
				private ["_position", "_obj_direction", "_dif_direction", "_new_direction", "_distance", "_rotated_position"];
				detach _x;
				_position = getPosASL _x;
				_obj_direction = ([_center_position, _position] call BIS_fnc_dirTo);
				_dif_direction = if (DZE_4) then {10} else {-10};
				_new_direction = _obj_direction + _dif_direction;
				_distance = [_center_position, _position] call BIS_fnc_distance2D;
				_obj_direction = getDir _x;
				_rotated_position = [_center_position, _distance, _new_direction] call BIS_fnc_relPos;
				_rotated_position set [2, _position select 2];
				_x setPosASL _rotated_position;
				_x attachTo [player];
				_x setDir (((_obj_direction - _player_direction) + _dif_direction) % 360);
				true
			} count _base_objects;
			DZE_4 = false;
			DZE_6 = false;
		};
		if(DZE_5) exitWith {
			_finished = true;
			_place = true;
		};
		if (DZE_cancelBuilding) exitWith {
			_finished = true;
			_place = false;
		};
		sleep 0.1;
	};
	{detach _x; true} count _base_objects;
	BCCurrentBase = _base_objects;
	if (_place) then {
		showCommandingMenu "#USER:BCBaseSaveMenu";
	} else {
		call fn_BCCancelBase;
	};
};

fn_BCGetDimensions = {
	private ["_xmin", "_xmax", "_ymin", "_ymax"];
	_xmin = 0;
	_xmax = 0;
	_ymin = 0;
	_ymax = 0;
	{
		private ["_position"];
		_position = _x select 1;
		if ((_position select 0) < _xmin) then {
			_xmin = _position select 0;
		};
		if ((_position select 0) > _xmax) then {
			_xmax = _position select 0;
		};
		
		if ((_position select 1) < _ymin) then {
			_ymin = _position select 1;
		};
		if ((_position select 1) > _ymax) then {
			_ymax = _position select 1;
		};
		true
	} count _this;
	[abs _xmin + abs _xmax, abs _ymin + abs _ymax]
};

BCMainMenu =
[
	["Base Manager",true],
	["Insert", 		[2], "", -5, [["expression", "[] spawn fn_BCInsert"]], "1", "1"],
	["========", 	[3], "", -5, [["expression", ""]], "1", "1"],
	["Set Center", 	[4], "", -5, [["expression", "[] spawn fn_BCSetCenter"]], "1", "1"],
	["Set Radius", 	[5], "", -5, [["expression", "[] spawn fn_BCSetRadius"]], "1", "1"],
	["========", 	[6], "", -5, [["expression", ""]], "1", "1"],
	["Copy",		[7], "", -5, [["expression", "[] spawn fn_BCCopy"]], "1", "1"],
	["Paste", 		[8], "", -5, [["expression", "[] spawn fn_BCPaste"]], "1", "1"],
	["Delete", 		[9], "", -5, [["expression", "[] spawn fn_BCDelete"]], "1", "1"],
	["========", 	[10], "", -5, [["expression", ""]], "1", "1"],
	["Exit", 		[11], "", -5, [["expression", ""]], "1", "1"]
];

BCBaseSaveMenu = [
	["",true],
		["Save", 	[2], "", -5, [["expression", "[] spawn fn_BCSaveToDb"]], "1", "1"],
		["Exit", 	[3], "", -5, [["expression", "[] spawn fn_BCCancelBase"]], "1", "1"]
];

showCommandingMenu "#USER:BCMainMenu";